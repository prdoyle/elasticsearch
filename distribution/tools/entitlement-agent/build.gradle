/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

apply plugin: 'elasticsearch.build'

dependencies {
  implementation 'org.ow2.asm:asm:9.7'
  testImplementation project(":test:framework")
}

tasks.named('test').configure {
  systemProperty "tests.security.manager", "false"
  dependsOn('jar')

  def agentJar = tasks.named('jar').flatMap { it.archiveFile }.get()
  jvmArgs "-javaagent:${agentJar}"
  classpath -= sourceSets.main.output // Don't _also_ put the classes on the classpath
}

tasks.named('jar').configure {
  manifest {
    attributes(
      'Premain-Class': 'org.elasticsearch.entitlement.agent.EntitlementAgent'
      , 'Can-Retransform-Classes': 'true'
    )
  }
}

tasks.named('forbiddenApisMain').configure {
  replaceSignatureFiles 'jdk-signatures'
}

